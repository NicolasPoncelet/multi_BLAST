from script import output_files, blast_results
import yaml
from pathlib import Path

configfile: "config/config.yaml"

# Chargement de la configuration
with open(Path("config/config.yaml"), "r") as infile:
    config_yaml = yaml.load(infile, Loader=yaml.SafeLoader)

ASSEMBLY_DIR = Path(config_yaml["assemblies_dir"]).resolve()
QUERY_DIR = Path(config_yaml["queries_dir"]).resolve()
OUTPUT_DIR = Path(config_yaml["analysis_dir"]).resolve()

# ---------------------------------------------------------------------------------
# RÃ¨gles Snakemake
# ---------------------------------------------------------------------------------

rule all:
    input:
        output_files.get()

rule move_fasta:
    """Move FASTA file into db dir. """
    input:
        assemblies = f"{ASSEMBLY_DIR}/{{assembly}}.fasta",
    output:
        fasta_files = f'{OUTPUT_DIR}/Database/{{assembly}}.fasta'
    shell:
        """
        cp {input.assemblies} {output.fasta_files}
        """

rule make_BLAST_db:
    """Create BLAST database from assembly."""
    input:
        assemblies = rules.move_fasta.output.fasta_files 

    output:
        db_nto = f'{OUTPUT_DIR}/Database/{{assembly}}.nto',
        db_ntf = f'{OUTPUT_DIR}/Database/{{assembly}}.ntf',
        db_nsq = f'{OUTPUT_DIR}/Database/{{assembly}}.nsq',
        db_not = f'{OUTPUT_DIR}/Database/{{assembly}}.not',
        db_njs = f'{OUTPUT_DIR}/Database/{{assembly}}.njs',
        db_nin = f'{OUTPUT_DIR}/Database/{{assembly}}.nin',
        db_nhr = f'{OUTPUT_DIR}/Database/{{assembly}}.nhr',
    conda:
        "envs/blast.yaml"
    shell:
        """
        makeblastdb \
        -in {input.assemblies} \
        -dbtype nucl \
        -out {OUTPUT_DIR}/Database/{wildcards.assembly}
        """

rule BLAST_query:
    """BLAST query in database"""
    input:
        db_files=rules.make_BLAST_db.output, 
        query=f"{QUERY_DIR}/{{query}}.fasta",
        assemblies=rules.move_fasta.output
    output:
        blast_out=f"{OUTPUT_DIR}/BLAST/{{assembly}}/{{query}}_out_blast.txt"
    conda:
        "envs/blast.yaml"
    params:
        header = config_yaml["header_report"]
    shell:
        """
        blastn \
        -task blastn \
        -query {input.query} \
        -db {OUTPUT_DIR}/Database/{wildcards.assembly} \
        -out {output.blast_out} \
        -evalue 1e-5 \
        -outfmt '6 {params.header}'
        """

rule BLAST_report:
    input:
        blasted_queries = expand(
            f"{OUTPUT_DIR}/BLAST/{{assembly}}/{{query}}_out_blast.txt",
            assembly=[a.stem for a in ASSEMBLY_DIR.glob("*.fasta")],
            query=[q.stem for q in QUERY_DIR.glob("*.fasta")]
        )
    output:
        compiled_report = f'{OUTPUT_DIR}/BLAST/compiled_results.csv'
    params:
        header = config_yaml["header_report"]  
    run:
        header_report = params.header.split()
        blast_results.compile( 
            f'{OUTPUT_DIR}/BLAST', 
            output.compiled_report, 
            header_report
        ) 



